# Global variables
FUZZER_NAME := "fuzzing_baremetal"
TARGET_DIR  := "target/QemuBaremetal"
BUILD_DIR   :=  TARGET_DIR + "/build"
BINARY_DIR  :=  BUILD_DIR + "/debug"
PROFILE     := "dev"

# Paths to QEMU Virtual Hardware for the kernel and the virtual disk
KERNEL    := TARGET_DIR + "/kernel.elf"
DUMMY_IMG := TARGET_DIR + "/virtualDisk.qcow2"

# Output Director
target_dir:
  @echo "[LOG] Creating the directory: {{TARGET_DIR}}"
  mkdir -p {{TARGET_DIR}}

# Creating a dummy virtual disk Image (32 MB)
image: target_dir
  @echo "[LOG] Creating the Virtual Disk: {{DUMMY_IMG}}"
  qemu-img create -f qcow2 {{DUMMY_IMG}} 32M

# Compiles the ARM kernel
target flavor: image
  @echo "[LOG] Compiling kernel.elf for flavor={{flavor}}"
  arm-none-eabi-gcc -ggdb -ffreestanding -nostartfiles -lgcc \
    -T example/mps2_m3.ld                                    \
    -mcpu=cortex-m3                                          \
    -D "TARGET_{{uppercase(flavor)}}"                        \
    -I example/include                                       \
    example/harness.c example/startup.c                      \
    -o {{KERNEL}}

# Building the Fuzzer binary
build flavor="breakpoint" ui="standard": target_dir
  @echo "[LOG] Building Fuzzer (profile={{PROFILE}}, flavor={{flavor}}, ui={{ui}})"
  cargo build                                                             \
    --profile {{PROFILE}}                                                 \
    --no-default-features                                                 \
    --features std,{{flavor}}{{ if ui == "tui" { ",tui" } else { "" } }}  \
    --target-dir {{BUILD_DIR}}                                            \

# Run the fuzzer binary under QEMU
run flavor="breakpoint" ui="standard": (target flavor) (build flavor ui)
  @echo "[LOG] Running the Fuzzer binary under QEMU"
  KERNEL={{KERNEL}}                                \
  DUMMY_IMG={{DUMMY_IMG}}                          \
  {{BINARY_DIR}}/{{FUZZER_NAME}}                   \
    -icount shift=auto,align=off,sleep=off         \
    -machine mps2-an385                            \
    -monitor null                                  \
    -kernel {{KERNEL}}                             \
    -drive if=none,format=qcow2,file={{DUMMY_IMG}} \
    -serial null                                   \
    -nographic                                     \
    -snapshot                                      \
    -S

# Quick smoke test for one flavor
test_flavor flavor: (target flavor) (build flavor)
    @echo "[LOG] Testing flavor={{flavor}}"
    TMP=$(mktemp -d) &&                                     \
    timeout 20s {{BINARY_DIR}}/{{FUZZER_NAME}}              \
      -icount shift=auto,align=off,sleep=off                \
      -machine mps2-an385                                   \
      -monitor null                                         \
      -kernel {{KERNEL}}                                    \
      -drive if=none,format=qcow2,file={{DUMMY_IMG}}        \
      -serial null                                          \
      -nographic                                            \
      -snapshot                                             \
      -S                                                    \
      | tee $$TMP/fuzz.log 2>&1 || true;                    \
    if grep -q Objective $$TMP/fuzz.log; then               \
      echo "✅ flavor={{flavor}} FOUND objective";          \
    else                                                    \
      echo "❌ flavor={{flavor}} MISSED objective"; exit 1; \
    fi

# Run test for all flavors
test: (test_flavor "low_level") (test_flavor "breakpoint") (test_flavor "sync_exit")

clean:
  @echo "[LOG] Cleaning Cargo Build"
  cargo clean
